plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

apply from: 'publish-info.gradle'

ext {
    projectEncoding = 'UTF-8'

    ossrhUsername = System.getenv("OSSRH_USERNAME")
    ossrhPassword = System.getenv("OSSRH_PASSWORD")
}

// Util Group
group'kr.devis.util'
version PUBLISH_VERSION
java.sourceCompatibility = JavaVersion.VERSION_1_8

repositories {
    mavenCentral()
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = PUBLISH_ARTIFACT_ID
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = PUBLISH_ARTIFACT_ID
                description = PUBLISH_DESCRIPTION
                url = PUBLISH_URL
                groupId = PUBLISH_GROUP_ID
                artifactId = PUBLISH_ARTIFACT_ID
                version = PUBLISH_VERSION

//                properties = []
                licenses {
                    license {
                        name = PUBLISH_LICENSE_NAME
                        url = PUBLISH_LICENSE_URL
                    }
                }
                developers {
                    developer {
                        id = PUBLISH_DEVELOPER_ID
                        name = PUBLISH_DEVELOPER_NAME
                        email = PUBLISH_DEVELOPER_EMAIL
                    }
                }
                scm {
                    connection = PUBLISH_SCM_CONNECTION
                    developerConnection = PUBLISH_SCM_DEVELOPER_CONNECTION
                    url = PUBLISH_SCM_URL
                }
            }
        }
    }
    repositories {
        maven {
            name = 'OSSRH'
            credentials {
                username = ossrhUsername ?: ""
                password = ossrhPassword ?: ""
            }
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

//            def releasesRepoUrl = layout.buildDirectory.dir('repos/releases')
//            def snapshotsRepoUrl = layout.buildDirectory.dir('repos/snapshots')
//            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
        }
    }
}


signing {
    sign publishing.publications.mavenJava
}

// 컴파일 인코딩 설정
[compileJava, compileTestJava]*.options*.encoding = "$projectEncoding"

// https://docs.gradle.org/current/userguide/publishing_setup.html#publishing_overview 참고
// ':signMavenJavaPublication' because it has no configured signatory: 키서버에 키가 없음